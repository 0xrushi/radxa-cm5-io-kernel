# Maintainer: dev-null2019
pkgname=radxa-cm5-uconsole-dtb
pkgver=6.1.43
pkgrel=1
pkgdesc="Device tree overlays for radxa cm5 uconsole"
arch=('aarch64')
url="https://github.com/dev-null2019/radxa-cm5-uconsole"
license=('GPL-2.0-only')
makedepends=('dtc')
source=("https://github.com/dev-null2019/radxa-cm5-uconsole/archive/main.tar.gz")
md5sums=('6be835567ca3d7e4bfd81c1cadd46035')

build() {
  cd "radxa-cm5-uconsole-main/devicetree_overlays"
  
  # Use cpp to handle includes for DTS files
  INCLUDE_PATH="/usr/src/linux-6.1.43/include"
  if [ ! -d "$INCLUDE_PATH" ]; then
    # Fallback to current kernel headers if not found at specific path
    INCLUDE_PATH="/usr/lib/modules/$(uname -r)/build/include"
  fi

  for dts in *.dts; do
    dtbo="${dts%.dts}.dtbo"
    echo "Compiling $dts..."
    cpp -nostdinc -I "$INCLUDE_PATH" -I "$INCLUDE_PATH/dt-bindings" -undef -x assembler-with-cpp "$dts" | \
    dtc -I dts -O dtb -o "$dtbo" -b 0 -@ -
  done
}

package() {
  cd "radxa-cm5-uconsole-main/devicetree_overlays"
  install -d "${pkgdir}/boot/dtbs/rockchip/overlays"
  install -m644 *.dtbo "${pkgdir}/boot/dtbs/rockchip/overlays/"
}
